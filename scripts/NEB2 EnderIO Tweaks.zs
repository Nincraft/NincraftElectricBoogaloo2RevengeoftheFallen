# COMMON VARIABLES
#------------------
var gasPipe                   = <Mekanism:PartTransmitter:8>;
var meGlassCable              = <appliedenergistics2:item.ItemMultiPart:16>;
var dimensionalTransceiver    = <EnderIO:blockTransceiver>;
var enderIO                   = <EnderIO:blockEnderIo>;
var travelAnchor              = <EnderIO:blockTravelAnchor>;
var poweredSpawner            = <EnderIO:blockPoweredSpawner>;
var conduitBinder             = <EnderIO:itemMaterial:1>;
var machineChassis            = <EnderIO:itemMachinePart:0>;
var soulVial                  = <EnderIO:itemSoulVessel>;
var basicCapacitor            = <EnderIO:itemBasicCapacitor:0>;
var doubleCapacitor           = <EnderIO:itemBasicCapacitor:1>;
var octadicCapacitor          = <EnderIO:itemBasicCapacitor:2>;
var enderAirBottle            = <Botania:manaResource:15>;

# Conduits
var redstoneConduit           = <EnderIO:itemRedstoneConduit:0>;
var conduitSwitch             = <EnderIO:itemRedstoneConduit:1>;
var insulatedRedstoneConduit  = <EnderIO:itemRedstoneConduit:2>;
var energyConduit             = <EnderIO:itemPowerConduit:0>;
var enhancedEnergyConduit     = <EnderIO:itemPowerConduit:1>;
var enderEnergyConduit        = <EnderIO:itemPowerConduit:2>;
var fluidConduit              = <EnderIO:itemLiquidConduit:0>;
var pressurizedFluidConduit   = <EnderIO:itemLiquidConduit:1>;
var enderFluidConduit         = <EnderIO:itemLiquidConduit:2>;
var itemConduit               = <EnderIO:itemItemConduit:0>;
var gasConduit                = <EnderIO:itemGasConduit:0>;
var meConduit                 = <EnderIO:itemMEConduit:0>;
var denseMEConduit            = <EnderIO:itemMEConduit:1>;

# Machines
var sagMill                   = <EnderIO:blockSagMill>;
var alloySmelter              = <EnderIO:blockAlloySmelter>;
var farmingStation            = <EnderIO:blockFarmStation>;

# Capacitor Banks
var basicCapacitorBank        = <EnderIO:blockCapBank:1>;
var capacitorBank             = <EnderIO:blockCapBank:2>;
var vibrantCapacitorBank      = <EnderIO:blockCapBank:3>;

# Thermal Dynamics Ducts
var leadstoneFluxduct         = <ThermalDynamics:ThermalDynamics_0:0>;
var hardenedFluxduct          = <ThermalDynamics:ThermalDynamics_0:1>;
var redstoneFluxduct          = <ThermalDynamics:ThermalDynamics_0:2>;
var redstoneFluxductEmpty     = <ThermalDynamics:ThermalDynamics_0:3>;
var resonantFluxduct          = <ThermalDynamics:ThermalDynamics_0:4>;
var resonantFluxductEmpty     = <ThermalDynamics:ThermalDynamics_0:5>;
var cryoStableFluxduct        = <ThermalDynamics:ThermalDynamics_0:6>;
var cryoStableFluxductEmpty   = <ThermalDynamics:ThermalDynamics_0:7>;
var temperateFluiduct         = <ThermalDynamics:ThermalDynamics_16:0>;
var temperateFluiductOpaque   = <ThermalDynamics:ThermalDynamics_16:1>;
var hardenedFluiduct          = <ThermalDynamics:ThermalDynamics_16:2>;
var hardenedFluiductOpaque    = <ThermalDynamics:ThermalDynamics_16:3>;
var fluxPlatedFluiduct        = <ThermalDynamics:ThermalDynamics_16:4>;
var fluxPlatedFluiductOpaque  = <ThermalDynamics:ThermalDynamics_16:5>;
var itemduct                  = <ThermalDynamics:ThermalDynamics_32:0>;
var itemductOpaque            = <ThermalDynamics:ThermalDynamics_32:1>;
var impulseItemduct           = <ThermalDynamics:ThermalDynamics_32:2>;
var impulseItemductOpaque     = <ThermalDynamics:ThermalDynamics_32:3>;
var warpItemduct              = <ThermalDynamics:ThermalDynamics_32:4>;
var warpItemductOpaque        = <ThermalDynamics:ThermalDynamics_32:5>;
var fluctuatingItemduct       = <ThermalDynamics:ThermalDynamics_32:6>;
var fluctuatingItemductOpaque = <ThermalDynamics:ThermalDynamics_32:7>;
var structuralduct            = <ThermalDynamics:ThermalDynamics_48:0>;

# Thermal Expansion Machines
var redstoneFurnace           = <ThermalExpansion:Machine:0>;
var pulverizer                = <ThermalExpansion:Machine:1>;
var inductionSmelter          = <ThermalExpansion:Machine:3>;

# Thermal Expansion Frames
var leadstoneCellFrame        = <ThermalExpansion:Frame:4>;
var hardenedCellFrame         = <ThermalExpansion:Frame:5>;
var redstoneCellFrame         = <ThermalExpansion:Frame:7>;
var resonantCellFrame         = <ThermalExpansion:Frame:9>;

# Thermal Expansion Energy Cells
var leadstoneEnergyCell       = <ThermalExpansion:Cell:1>;
var hardenedEnergyCell        = <ThermalExpansion:Cell:2>;
var redstoneEnergyCell        = <ThermalExpansion:Cell:3>;
var resonantEnergyCell        = <ThermalExpansion:Cell:4>;

# MineFactory Machines
var planter                   = <MineFactoryReloaded:machine.0:0>;
var harvester                 = <MineFactoryReloaded:machine.0:2>;

# ORE DICTIONARY
#----------------
var anyDiamond                = <ore:gemDiamond>;
var anyInolashiteIngot        = <ore:ingotInolashite>;
var anyEnergeticAlloyIngot    = <ore:ingotEnergeticAlloy>;
var anyVibrantAlloyIngot      = <ore:ingotPhasedGold>;
var anyEnderiumIngot          = <ore:ingotEnderium>;
var anyRedstoneAlloyIngot     = <ore:ingotRedstoneAlloy>;
var anyEnderiumFrame          = <ore:frameGtEnderium>;
var anyPlasticPipe            = <ore:pipePlastic>;
var anyPlatinumIngot          = <ore:ingotPlatinum>;
var anyRedNetCable            = <ore:cableRedNet>;
var anyRedNetEnergyCable      = <ore:cableRedNetEnergy>;
var anyPlasticSheet           = <ore:sheetPlastic>;
var anyHardenedGlass          = <ore:blockGlassHardened>;
var anyShadowSteelIngot       = <ore:ingotShadowSteel>;
var anySoulariumIngot         = <ore:ingotSoularium>;
var anyVulcaniteIngot         = <ore:ingotVulcanite>;

# RECIPE TWEAKS
#---------------

# Basic Capacitor Bank
recipes.removeShaped(basicCapacitorBank);
recipes.addShaped(basicCapacitorBank, [
    [anyPlatinumIngot, basicCapacitor,     anyPlatinumIngot],
    [basicCapacitor,   leadstoneCellFrame, basicCapacitor],
    [anyPlatinumIngot, basicCapacitor,     anyPlatinumIngot]]);

# Capacitor Bank
recipes.removeShaped(capacitorBank);
recipes.addShaped(capacitorBank, [
    [anyShadowSteelIngot, doubleCapacitor,   anyShadowSteelIngot],
    [doubleCapacitor,     hardenedCellFrame, doubleCapacitor],
    [anyShadowSteelIngot, doubleCapacitor,   anyShadowSteelIngot]]);

# Vibrant Capacitor Bank
recipes.removeShaped(vibrantCapacitorBank);
recipes.addShaped(vibrantCapacitorBank, [
    [anyVibrantAlloyIngot, octadicCapacitor,  anyVibrantAlloyIngot],
    [octadicCapacitor,     resonantCellFrame, octadicCapacitor],
    [anyVibrantAlloyIngot, octadicCapacitor,  anyVibrantAlloyIngot]]);

# Alloy Smelter
var basicInductionSmelter     = inductionSmelter.onlyWithTag({Level:0 as byte});
recipes.remove(alloySmelter);
recipes.addShaped(alloySmelter, [
    [anyInolashiteIngot, anyVulcaniteIngot,     anyInolashiteIngot],
    [anyVulcaniteIngot,  basicInductionSmelter, anyVulcaniteIngot],
    [anyInolashiteIngot, null,                  anyInolashiteIngot]]);

# SAG Mill
var basicPulverizer           = pulverizer.onlyWithTag({Level:0 as byte});
recipes.remove(sagMill);
recipes.addShaped(sagMill, [
    [anyInolashiteIngot, anyDiamond,      anyInolashiteIngot],
    [anyDiamond,         basicPulverizer, anyDiamond],
    [anyInolashiteIngot, anyDiamond,      anyInolashiteIngot]]);

# Farming Station
recipes.remove(farmingStation);
recipes.addShapedMirrored(farmingStation, [
    [anyInolashiteIngot, null,           anyInolashiteIngot],
    [harvester,          machineChassis, planter],
    [anyInolashiteIngot, null,           anyInolashiteIngot]]);

# Make Conduits Cost Other Mod Pipes
recipes.remove(redstoneConduit);
recipes.remove(insulatedRedstoneConduit);
recipes.remove(energyConduit);
recipes.remove(enhancedEnergyConduit);
recipes.remove(enderEnergyConduit);
recipes.remove(fluidConduit);
recipes.remove(pressurizedFluidConduit);
recipes.remove(enderFluidConduit);
recipes.remove(itemConduit);
recipes.remove(gasConduit);
recipes.remove(meConduit);
#recipes.remove(denseMEConduit);

recipes.addShaped(redstoneConduit * 8, [
    [conduitBinder,         null,                  conduitBinder],
    [anyRedstoneAlloyIngot, anyRedstoneAlloyIngot, anyRedstoneAlloyIngot],
    [conduitBinder,         null,                  conduitBinder]]);

recipes.addShaped(insulatedRedstoneConduit * 8, [
    [conduitBinder,  null,           conduitBinder],
    [anyRedNetCable, anyRedNetCable, anyRedNetCable],
    [conduitBinder,  null,           conduitBinder]]);

recipes.addShaped(energyConduit * 8, [
    [conduitBinder,        null,                 conduitBinder],
    [anyRedNetEnergyCable, anyRedNetEnergyCable, anyRedNetEnergyCable],
    [conduitBinder,        null,                 conduitBinder]]);

recipes.addShaped(enhancedEnergyConduit * 8, [
    [conduitBinder,        conduitBinder,          conduitBinder],
    [anyRedNetEnergyCable, anyEnergeticAlloyIngot, anyRedNetEnergyCable],
    [conduitBinder,        conduitBinder,          conduitBinder]]);

recipes.addShaped(enderEnergyConduit * 8, [
    [conduitBinder,        conduitBinder,        conduitBinder],
    [anyRedNetEnergyCable, anyVibrantAlloyIngot, anyRedNetEnergyCable],
    [conduitBinder,        conduitBinder,        conduitBinder]]);

recipes.addShaped(fluidConduit * 8, [
    [conduitBinder,  null,           conduitBinder],
    [anyPlasticPipe, anyPlasticPipe, anyPlasticPipe],
    [conduitBinder,  null,           conduitBinder]]);

recipes.addShaped(pressurizedFluidConduit * 8, [
    [conduitBinder,  anyHardenedGlass, conduitBinder],
    [anyPlasticPipe, anyPlasticPipe,   anyPlasticPipe],
    [conduitBinder,  anyHardenedGlass, conduitBinder]]);

recipes.addShaped(enderFluidConduit * 8, [
    [conduitBinder,  anyHardenedGlass,     conduitBinder],
    [anyPlasticPipe, anyVibrantAlloyIngot, anyPlasticPipe],
    [conduitBinder,  anyHardenedGlass,     conduitBinder]]);

recipes.addShaped(itemConduit * 8, [
    [conduitBinder, null,         conduitBinder],
    [warpItemduct,  warpItemduct, warpItemduct],
    [conduitBinder, null,         conduitBinder]]);
recipes.addShaped(itemConduit * 8, [
    [conduitBinder,      null,               conduitBinder],
    [warpItemductOpaque, warpItemductOpaque, warpItemductOpaque],
    [conduitBinder,      null,               conduitBinder]]);

recipes.addShaped(gasConduit * 8, [
    [conduitBinder, conduitBinder, conduitBinder],
    [gasPipe,       gasPipe,       gasPipe],
    [conduitBinder, conduitBinder, conduitBinder]]);

recipes.addShaped(meConduit * 8, [
    [conduitBinder, null,         conduitBinder],
    [meGlassCable,  meGlassCable, meGlassCable],
    [conduitBinder, null,         conduitBinder]]);

# Soul Vial
recipes.removeShaped(soulVial);
recipes.addShaped(soulVial, [
    [null,             anySoulariumIngot, null],
    [anyHardenedGlass, enderAirBottle,    anyHardenedGlass],
    [null,             anyHardenedGlass,  null]]);
